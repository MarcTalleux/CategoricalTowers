#! @Chunk ConcreteCategoryForCAP

LoadPackage( "CatReps" );

#! @Example
c3c3 := ConcreteCategoryForCAP( [ [2,3,1], [4,5,6], [,,,5,6,4] ] );
#! A finite concrete category
objects := SetOfObjects( c3c3 );
#! [ An object in subcategory given by: <An object in FinSets>,
#!   An object in subcategory given by: <An object in FinSets> ]
Perform( objects, Display );
#! An object in subcategory given by: [ 1, 2, 3 ]
#! An object in subcategory given by: [ 4, 5, 6 ]
gmorphisms := SetOfGeneratingMorphisms( c3c3 );
#! [ A morphism in subcategory given by: <A morphism in FinSets>,
#!   A morphism in subcategory given by: <A morphism in FinSets>,
#!   A morphism in subcategory given by: <A morphism in FinSets> ]
Perform( gmorphisms, Display );
#! A morphism in subcategory given by:
#! [ [ 1, 2, 3 ], [ [ 1, 2 ], [ 2, 3 ], [ 3, 1 ] ], [ 1, 2, 3 ] ]
#! A morphism in subcategory given by:
#! [ [ 1, 2, 3 ], [ [ 1, 4 ], [ 2, 5 ], [ 3, 6 ] ], [ 4, 5, 6 ] ]
#! A morphism in subcategory given by:
#! [ [ 4, 5, 6 ], [ [ 4, 5 ], [ 5, 6 ], [ 6, 4 ] ], [ 4, 5, 6 ] ]
qc3c3 := RightQuiver( "q(2)[a:1->1,b:1->2,c:2->2]" );
#! q(2)[a:1->1,b:1->2,c:2->2]
GF3 := HomalgRingOfIntegers( 3 );
#! GF(3)
GF3q := PathAlgebra( GF3, qc3c3 );
#! GF(3) * q
rel := [GF3q.a^3-GF3q.1, GF3q.c^3-GF3q.2, GF3q.a*GF3q.b-GF3q.b*GF3q.c];;
kq := Algebroid( GF3q, rel );
#! Algebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
#! @EndExample

#! Now we add the bialgebroid structure:

#! @Example
counit := rec( a := 1, b := 1, c := 1 );
#! rec( a := 1, b := 1, c := 1 )
kq2 := kq^2;
#! Algebroid generated by the right quiver qxq(1x1,1x2,2x1,2x2)\
#! [1xa:1x1->1x1,1xb:1x1->1x2,1xc:1x2->1x2,2xa:2x1->2x1,2xb:2x1->2x2,2xc:2x2->2x2,\
#! ax1:1x1->1x1,ax2:1x2->1x2,bx1:1x1->2x1,bx2:1x2->2x2,cx1:2x1->2x1,cx2:2x2->2x2]
PreCompose( kq2.ax1, kq2.1xa ) = PreCompose( kq2.1xa, kq2.ax1 );
#! true
PreCompose( kq2.bx1, kq2.2xb ) = PreCompose( kq2.1xb, kq2.bx2 );
#! true
PreCompose( kq2.cx2, kq2.2xc ) = PreCompose( kq2.2xc, kq2.cx2 );
#! true
comult := rec( a := PreCompose( kq2.ax1, kq2.1xa ),
               b := PreCompose( kq2.1xb, kq2.bx2 ),
               c := PreCompose( kq2.cx2, kq2.2xc ) );
#! rec( a := (1x1)-[{ Z(3)^0*(1xa*ax1) }]->(1x1),
#!      b := (1x1)-[{ Z(3)^0*(1xb*bx2) }]->(2x2),
#!      c := (2x2)-[{ Z(3)^0*(2xc*cx2) }]->(2x2) )
AddBialgebroidStructure( kq, counit, comult );
#! Bialgebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
counit := Counit( kq );
#! Functor from finitely presented Bialgebroid generated by
#! the right quiver q(2)[a:1->1,b:1->2,c:2->2] ->
#! Algebra generated by the right quiver *(1)[]
comult := Comultiplication( kq );
#! Functor from finitely presented Bialgebroid generated by
#! the right quiver q(2)[a:1->1,b:1->2,c:2->2] ->
#! Algebroid generated by the right quiver qxq(1x1,1x2,2x1,2x2)
#! [1xa:1x1->1x1,1xb:1x1->1x2,1xc:1x2->1x2,2xa:2x1->2x1,2xb:2x1->2x2,2xc:2x2->2x2,\
#! ax1:1x1->1x1,ax2:1x2->1x2,bx1:1x1->2x1,bx2:1x2->2x2,cx1:2x1->2x1,cx2:2x2->2x2]
#! @EndExample

#! @Example
kmat := MatrixCategory( GF3 );
#! Category of matrices over GF(3)
CatReps := CategoryOfRepresentations( kq, kmat );
#! The category of functors: Bialgebroid generated by the right quiver
#! q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over GF(3)
V1 := VectorSpaceObject( 5, GF3 );
#! <A vector space object over GF(3) of dimension 5>
V2 := VectorSpaceObject( 4, GF3 );
#! <A vector space object over GF(3) of dimension 4>
d := [[1,1,0,0,0],[0,1,1,0,0],[0,0,1,0,0],[0,0,0,1,1],[0,0,0,0,1]];;
d := HomalgMatrix( d, 5, 5, GF3 );;
d := VectorSpaceMorphism( V1, d, V1 );
#! <A morphism in Category of matrices over GF(3)>
Display( d );
#!  1 1 . . .
#!  . 1 1 . .
#!  . . 1 . .
#!  . . . 1 1
#!  . . . . 1
#! 
#! A morphism in Category of matrices over GF(3)
e := [[0,1,0,0],[0,0,1,0],[0,0,0,0],[0,1,0,1],[0,0,1,0]];;
e := HomalgMatrix( e, 5, 4, GF3 );;
e := VectorSpaceMorphism( V1, e, V2 );
#! <A morphism in Category of matrices over GF(3)>
Display( e );
#!  . 1 . .
#!  . . 1 .
#!  . . . .
#!  . 1 . 1
#!  . . 1 .
#! 
#! A morphism in Category of matrices over GF(3)
f := [[1,1,0,0],[0,1,1,0],[0,0,1,0],[0,0,0,1]];;
f := HomalgMatrix( f, 4, 4, GF3 );;
f := VectorSpaceMorphism( V2, f, V2 );
#! <A morphism in Category of matrices over GF(3)>
Display( f );
#!  1 1 . .
#!  . 1 1 .
#!  . . 1 .
#!  . . . 1
#! 
#! A morphism in Category of matrices over GF(3)
nine := AsObjectInHomCategory( kq, [ V1, V2 ], [ d, e, f ] );
#! <An object in The category of functors: Bialgebroid generated by the right quiver
#!  q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over GF(3)>
nine(kq.1);
#! <A vector space object over GF(3) of dimension 5>
nine(kq.2);
#! <A vector space object over GF(3) of dimension 4>
nine(kq.b);
#! <A morphism in Category of matrices over GF(3)>
Display( nine(kq.b) );
#!  . 1 . .
#!  . . 1 .
#!  . . . .
#!  . 1 . 1
#!  . . 1 .
#! 
#! A morphism in Category of matrices over GF(3)
IsWellDefined( nine );
#! true
fortyone := TensorProductOnObjects( nine, nine );
#! <An object in The category of functors: Bialgebroid generated by the right quiver
#!  q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over GF(3)>
IsWellDefined( fortyone );
#! true
fortyone(kq.a) = TensorProductOnMorphisms( nine(kq.a), nine(kq.a) );
#! true
fortyone(kq.b) = TensorProductOnMorphisms( nine(kq.b), nine(kq.b) );
#! true
fortyone(kq.c) = TensorProductOnMorphisms( nine(kq.c), nine(kq.c) );
#! true
#! @EndExample
