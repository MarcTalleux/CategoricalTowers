#! @BeginChunk Precompilation

#! @Example

LoadPackage( "Algebroids" );
#! true

QQ := HomalgFieldOfRationals( );;
snake_quiver := RightQuiver( "Q(4)[a:1->2,b:2->3,c:3->4]" );;
A := PathAlgebra( QQ, snake_quiver );;
A_bar := QuotientOfPathAlgebra( A, [ A.abc ] );;

ReadPackage( "Algebroids", "gap/CompilerLogic.gi" );
#! true

precompile_AdditiveClosureOfAlgebroid := function( Rq, over_Z, path_algebra, ring )
    
    CapJitPrecompileCategoryAndCompareResult(
        EvalString( ReplacedString( """Rq -> AdditiveClosure( Algebroid(
            Rq, over_Z : FinalizeCategory := true
        ) )""", "over_Z", String( over_Z ) ) ),
        [ Rq ],
        "Algebroids",
        Concatenation(
            "AdditiveClosureOfAlgebroidOfFiniteDimensional",
            path_algebra,
            "OfRightQuiverOver",
            ring,
            "Precompiled"
        ) :
        operations := "primitive"
    ); end;;

precompile_AdditiveClosureOfAlgebroid( A, false, "PathAlgebra", "Field" );
precompile_AdditiveClosureOfAlgebroid( A_bar, false, "QuotientOfPathAlgebra", "Field" );
precompile_AdditiveClosureOfAlgebroid( A, true, "PathAlgebra", "Z" );
precompile_AdditiveClosureOfAlgebroid( A_bar, true, "QuotientOfPathAlgebra", "Z" );

AdditiveClosureOfAlgebroidOfFiniteDimensionalPathAlgebraOfRightQuiverOverFieldPrecompiled( A );
#! Additive closure( Algebroid freely generated by the right quiver Q(4)[a:1->2,b\
#! :2->3,c:3->4] )

# check that the compiled code is loaded automatically
# for this, we use the name of the argument of `ZeroObject`:
# for non-compiled code it is "cat", while for compiled code it is "cat_1"

cat := AdditiveClosure( Algebroid( A, false ) );;
NamesLocalVariablesFunction( Last( cat!.added_functions.ZeroObject )[1] )[1] = "cat_1";
#! true

cat := AdditiveClosure( Algebroid( A_bar, false ) );;
NamesLocalVariablesFunction( Last( cat!.added_functions.ZeroObject )[1] )[1] = "cat_1";
#! true

cat := AdditiveClosure( Algebroid( A, true ) );;
NamesLocalVariablesFunction( Last( cat!.added_functions.ZeroObject )[1] )[1] = "cat_1";
#! true

cat := AdditiveClosure( Algebroid( A_bar, true ) );;
NamesLocalVariablesFunction( Last( cat!.added_functions.ZeroObject )[1] )[1] = "cat_1";
#! true

#! @EndExample

#! @EndChunk
