#! @BeginChunk IntegersAsMonoid

LoadPackage( "Algebroids" );

#! Create a right quiver $q$ with a single vertix $o$ and two arrows $m$, $i$.

#! @Example
q := RightQuiver( "q(o)[m:o->o,i:o->o]" );
#! q(o)[m:o->o,i:o->o]
L := [ [ q.m * q.i, q.o ], [ q.i * q.m, q.o ] ];
#! [ [ (m*i), (o) ], [ (i*m), (o) ] ]
#! @EndExample

#! Create a monoid from $q$ and the list $L$ of relations.

#! @Example
ZZ := Category( q, L );
#! Monoid generated by the right quiver q(o)[m:o->o,i:o->o]
IsCommutative( ZZ );
#! true
ZZ.o;
#! <(o)>
m := ZZ.m;
#! (o)-[(m)]->(o)
i := ZZ.i;
#! (o)-[(i)]->(o)
SetOfObjects( ZZ );
#! [ <(o)> ]
SetOfGeneratingMorphisms( ZZ );
#! [ (o)-[(m)]->(o), (o)-[(i)]->(o) ]
SetOfGeneratingMorphisms( ZZ, ZZ.o, ZZ.o );
#! [ (o)-[(m)]->(o), (o)-[(i)]->(o) ]
ObjectInFpCategory( ZZ, q.o ) = ZZ.o;
#! true
ZZ.o = q.o / ZZ;
#! true
MorphismInFpCategory( ZZ, q.m ) = ZZ.m;
#! true
ZZ.m = q.m / ZZ;
#! true
IdentityMorphism( ZZ.o );
#! (o)-[(o)]->(o)
#! @EndExample

#! We can compute in the algebroids.
#! For instance we can form compositions.

#! @Example
PreCompose( [ m, i, m, m, i, m, i, m ] );
#! (o)-[(m*m)]->(o)
unit := Unit( ZZ );
#! Functor from Monoid generated by the right quiver *(1)[]
#! -> Monoid generated by the right quiver q(o)[m:o->o,i:o->o]
mul := Multiplication( ZZ );
#! Functor from Monoid generated by the right quiver
#! qxq(oo)[om:oo->oo,oi:oo->oo,mo:oo->oo,io:oo->oo]
#! -> Monoid generated by the right quiver q(o)[m:o->o,i:o->o]
#! @EndExample

#! @EndChunk
