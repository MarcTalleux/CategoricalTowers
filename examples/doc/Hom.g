#! @System Hom

#! @Example
LoadPackage( "Bialgebroid", ">= 2017.06.06" );
#! true
q := RightQuiver( "q(1)[t:1->1]" );
#! q(1)[t:1->1]
Q := HomalgFieldOfRationals( );
#! Q
B := Algebroid( Q, q );
#! Algebra generated by the right quiver q(1)[t:1->1]
counit := rec( t := 1 );
#! rec( t := 1 )
B2 := B^2;
#! Algebra generated by the right quiver qxq(1x1)[1xt:1x1->1x1,tx1:1x1->1x1]
comult := rec( t := B2.tx1 );
#! rec( t := (1x1)-[{ 1*(tx1) }]->(1x1) )
AddBialgebroidStructure( B, counit, comult );
#! Bialgebra generated by the right quiver q(1)[t:1->1]
counit := Counit( B );
#! Functor from finitely presented Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Algebra generated by
#! the right quiver *(1)[]
ApplyFunctor( counit, B.1 );
#! (1)
ApplyFunctor( counit, B.t );
#! (1)-[1*(1)]->(1)
comult := Comultiplication( B );
#! Functor from finitely presented Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Algebra generated by
#! the right quiver qxq(1x1)[1xt:1x1->1x1,tx1:1x1->1x1]
ApplyFunctor( comult, B.1 );
#! (1x1)
ApplyFunctor( comult, B.t );
#! (1x1)-[{ 1*(tx1) }]->(1x1)
LoadPackage( "LinearAlgebraForCAP" );
#! true
A := MatrixCategory( Q );
#! Category of matrices over Q
LoadPackage( "FunctorCategories" );
#! true
H := Hom( B, A );
#! The category of functors from Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Category of matrices over Q
z := ZeroObject( H );
#! <A zero object in The category of functors from Bialgebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
z( B.1 );
#! <A vector space object over Q of dimension 0>
z( B.t );
#! <A zero, isomorphism in Category of matrices over Q>
idz := IdentityMorphism( z );
#! <A zero, identity morphism in The category of functors from Bialgebra
#!  generated by the right quiver q(1)[t:1->1] -> Category of matrices over Q>
idz( B.1 );
#! <A zero, identity morphism in Category of matrices over Q>
DirectSum( z, z );
#! <A zero object in The category of functors from Bialgebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
phi := HomalgMatrix( [ 0, 1, 0,  0, 0, 1,  0, -1, -2 ], 3, 3, Q );
#! <A 3 x 3 matrix over an internal ring>
V := VectorSpaceObject( 3, Q );
#! <A vector space object over Q of dimension 3>
V := rec( 1 := V, t := VectorSpaceMorphism( V, phi, V ) );
#! rec( 1 := <A vector space object over Q of dimension 3>,
#!      t := <A morphism in Category of matrices over Q> )
V := CapFunctorFromFinitelyPresentedAlgebroid( B, V );
#! Functor from finitely presented Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Category of matrices over Q
V := AsObjectInHomCategory( V );
#! <An object in The category of functors from Bialgebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
V( B.1 );
#! <A vector space object over Q of dimension 3>
V( B.t );
#! <A morphism in Category of matrices over Q>
W := DirectSum( V, V );
#! <An object in The category of functors from Bialgebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
W( B.1 );
#! <A vector space object over Q of dimension 6>
W( B.t );
#! <A morphism in Category of matrices over Q>
Display( W( B.t ) );
#! [ [   0,   1,   0,   0,   0,   0 ],
#!   [   0,   0,   1,   0,   0,   0 ],
#!   [   0,  -1,  -2,   0,   0,   0 ],
#!   [   0,   0,   0,   0,   1,   0 ],
#!   [   0,   0,   0,   0,   0,   1 ],
#!   [   0,   0,   0,   0,  -1,  -2 ] ]
#! 
#! A morphism in Category of matrices over Q
pi1 := ProjectionInFactorOfDirectSum( [ V, V ], 1 );
#! <A morphism in The category of functors from Bialgebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
pi1( B.1 );
#! <A morphism in Category of matrices over Q>
Display( pi1( B.1 ) );
#! [ [  1,  0,  0 ],
#!   [  0,  1,  0 ],
#!   [  0,  0,  1 ],
#!   [  0,  0,  0 ],
#!   [  0,  0,  0 ],
#!   [  0,  0,  0 ] ]
#! 
#! A morphism in Category of matrices over Q
#! @EndExample
