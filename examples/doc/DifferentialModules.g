#! @System DifferentialModules

LoadPackage( "FunctorCategories" );

#! @Example
q := RightQuiver( "q(1)[t:1->1]" );
#! q(1)[t:1->1]
Q := HomalgFieldOfRationals( );
#! Q
Qq := PathAlgebra( Q, q );
#! Q * q
B := Algebroid( Qq );
#! Algebra generated by the right quiver q(1)[t:1->1]
counit := rec( t := 0 );
#! rec( t := 0 )
B2 := B^2;
#! Algebra generated by the right quiver qxq(1x1)[1xt:1x1->1x1,tx1:1x1->1x1]
comult := rec( t := B2.tx1 + B2.1xt );
#! rec( t := (1x1)-[{ 1*(tx1) + 1*(1xt) }]->(1x1) )
AddBialgebroidStructure( B, counit, comult );
#! Bialgebra generated by the right quiver q(1)[t:1->1]
counit := Counit( B );
#! Functor from finitely presented Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Algebra generated by
#! the right quiver *(1)[]
ApplyFunctor( counit, B.1 );
#! (1)
ApplyFunctor( counit, B.t );
#! (1)-[0]->(1)
comult := Comultiplication( B );
#! Functor from finitely presented Bialgebra generated by
#! the right quiver q(1)[t:1->1] -> Algebra generated by
#! the right quiver qxq(1x1)[1xt:1x1->1x1,tx1:1x1->1x1]
ApplyFunctor( comult, B.1 );
#! (1x1)
ApplyFunctor( comult, B.t );
#! (1x1)-[{ 1*(tx1) + 1*(1xt) }]->(1x1)
antipode := rec( 1 := B.1, t := -B.t );
#! rec( 1 := (1), t := (1)-[-1*(t)]->(1) )
AddAntipode( B, antipode );
antipode := Antipode( B );
#! Cofunctor from finitely presented
#! Hopf algebra generated by the right quiver q(1)[t:1->1]
#! -> Hopf algebra generated by the right quiver q(1)[t:1->1]
ApplyFunctor( antipode, B.1 );
#! (1)
ApplyFunctor( antipode, B.t );
#! (1)-[-1*(t)]->(1)
LoadPackage( "LinearAlgebraForCAP" );
#! true
A := MatrixCategory( Q );
#! Category of matrices over Q
H := Hom( B, A );
#! The category of functors: Hopf algebra generated by
#! the right quiver q(1)[t:1->1] -> Category of matrices over Q
z := ZeroObject( H );
#! <A zero object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
z( B.1 );
#! <A vector space object over Q of dimension 0>
z( B.t );
#! <A zero, isomorphism in Category of matrices over Q>
idz := IdentityMorphism( z );
#! <A zero, identity morphism in The category of functors: Hopf algebra
#!  generated by the right quiver q(1)[t:1->1] -> Category of matrices over Q>
idz( B.1 );
#! <A zero, identity morphism in Category of matrices over Q>
DirectSum( z, z );
#! <A zero object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
phi := HomalgMatrix( [ 0, 1, 0,  0, 0, 1,  1, 0, 0 ], 3, 3, Q );
#! <A 3 x 3 matrix over an internal ring>
V := VectorSpaceObject( 3, Q );
#! <A vector space object over Q of dimension 3>
V := rec( 1 := V, t := VectorSpaceMorphism( V, phi, V ) );
#! rec( 1 := <A vector space object over Q of dimension 3>,
#!      t := <A morphism in Category of matrices over Q> )
V := AsObjectInHomCategory( B, V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
IsWellDefined( V );
#! true
V( B.1 );
#! <A vector space object over Q of dimension 3>
V( B.t );
#! <A morphism in Category of matrices over Q>
Display( V( B.t ) );
#! [ [  0,  1,  0 ],
#!   [  0,  0,  1 ],
#!   [  1,  0,  0 ] ]
#! 
#! A morphism in Category of matrices over Q
IsZero( V );
#! false
W := DirectSum( V, V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
W( B.1 );
#! <A vector space object over Q of dimension 6>
W( B.t );
#! <A morphism in Category of matrices over Q>
Display( W( B.t ) );
#! [ [  0,  1,  0,  0,  0,  0 ],
#!   [  0,  0,  1,  0,  0,  0 ],
#!   [  1,  0,  0,  0,  0,  0 ],
#!   [  0,  0,  0,  0,  1,  0 ],
#!   [  0,  0,  0,  0,  0,  1 ],
#!   [  0,  0,  0,  1,  0,  0 ] ]
#! 
#! A morphism in Category of matrices over Q
pi1 := ProjectionInFactorOfDirectSum( [ V, V ], 1 );
#! <A morphism in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
pi1 = -pi1;
#! false
pi1( B.1 );
#! <A morphism in Category of matrices over Q>
Display( pi1( B.1 ) );
#! [ [  1,  0,  0 ],
#!   [  0,  1,  0 ],
#!   [  0,  0,  1 ],
#!   [  0,  0,  0 ],
#!   [  0,  0,  0 ],
#!   [  0,  0,  0 ] ]
#! 
#! A morphism in Category of matrices over Q
IsWellDefined( pi1 );
#! true
IsEpimorphism( pi1 );
#! true
IsMonomorphism( pi1 );
#! false
V1 := KernelObject( pi1 );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
IsWellDefined( V1 );
#! true
V1 = V;
#! true
pi2 := ProjectionInFactorOfDirectSum( [ V, V ], 2 );
#! <A morphism in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
pi1 = pi2;
#! false
I := TensorUnit( H );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
I( B.1 );
#! <A vector space object over Q of dimension 1>
I( B.t );
#! <A morphism in Category of matrices over Q>
Display( I( B.t ) );
#! [ [  0 ] ]
#! 
#! A morphism in Category of matrices over Q
VV := TensorProductOnObjects( V, V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
VV( B.1 );
#! <A vector space object over Q of dimension 9>
VV( B.t );
#! <A morphism in Category of matrices over Q>
Display( VV( B.t ) );
#! [ [  0,  1,  0,  1,  0,  0,  0,  0,  0 ],
#!   [  0,  0,  1,  0,  1,  0,  0,  0,  0 ],
#!   [  1,  0,  0,  0,  0,  1,  0,  0,  0 ],
#!   [  0,  0,  0,  0,  1,  0,  1,  0,  0 ],
#!   [  0,  0,  0,  0,  0,  1,  0,  1,  0 ],
#!   [  0,  0,  0,  1,  0,  0,  0,  0,  1 ],
#!   [  1,  0,  0,  0,  0,  0,  0,  1,  0 ],
#!   [  0,  1,  0,  0,  0,  0,  0,  0,  1 ],
#!   [  0,  0,  1,  0,  0,  0,  1,  0,  0 ] ]
#! 
#! A morphism in Category of matrices over Q
Vs := DualOnObjects( V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
Vs( B.1 );
#! <A vector space object over Q of dimension 3>
Vs( B.t );
#! <A morphism in Category of matrices over Q>
Display( Vs( B.t ) );
#! [ [   0,   0,  -1 ],
#!   [  -1,   0,   0 ],
#!   [   0,  -1,   0 ] ]
#! 
#! A morphism in Category of matrices over Q
epsilon := MorphismToBidual( V );
#! <A morphism in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
Source( epsilon ) = V;
#! true
Range( epsilon ) = V;
#! true
EndV := InternalHomOnObjects( V, V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
VsV := TensorProductOnObjects( Vs, V );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
VVs := TensorProductOnObjects( V, Vs );
#! <An object in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
EndV = VsV;
#! true
EndV = VVs;
#! false
beta := Braiding( Vs, V );
#! <A morphism in The category of functors: Hopf algebra generated by
#!  the right quiver q(1)[t:1->1] -> Category of matrices over Q>
Source( beta ) = VsV;
#! true
Range( beta ) = VVs;
#! true
#! @EndExample
