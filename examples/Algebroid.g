#! @Chunk Algebroid

LoadPackage( "CatReps" );

#! We can also create finite concrete categories with objects
#! not starting with $1$, to demonstrate that
#! <C>ConcreteCategoryForCAP</C>( [ [,,,5,6,4], [,,,7,8,9], [,,,,,,8,9,7] ] )
#! and <C>ConcreteCategoryForCAP</C>( [ [2,3,1], [4,5,6], [,,,5,6,4] ] ) yield
#! isomorphic categories, in particular, their underlying quivers are isomorphic,
#! inducing isomorphic algebroids and isomorphic categories of representations.

#! @Example
ccat1 := ConcreteCategoryForCAP( [ [2,3,1], [4,5,6], [,,,5,6,4] ] );
#! A finite concrete category
ccat2 := ConcreteCategoryForCAP( [ [,,,5,6,4], [,,,7,8,9], [,,,,,,8,9,7] ] );
#! A finite concrete category
Q := HomalgFieldOfRationals( );
#! Q
A1 := Q[ccat1];
#! Algebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
UnderlyingCategory( A1 );
#! Category generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2] with relations
UnderlyingCategory( UnderlyingCategory( A1 ) ) = ccat1;
#! true
A2 := Q[ccat2];
#! Algebroid generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2]
UnderlyingCategory( A2 );
#! Category generated by the right quiver q(2)[a:1->1,b:1->2,c:2->2] with relations
UnderlyingCategory( UnderlyingCategory( A2 ) ) = ccat2;
#! true
CatReps1 := Hom( A1, Q );
#! The category of functors: Algebroid generated by the right quiver
#! q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over Q
CatReps2 := Hom( A2, Q );
#! The category of functors: Algebroid generated by the right quiver
#! q(2)[a:1->1,b:1->2,c:2->2] -> Category of matrices over Q
#! @EndExample
