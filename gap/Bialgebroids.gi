#
# Bialgebroids: Bialgebroids as preadditive categories generated by enhanced quivers
#
# Implementations
#

####################################
#
# representations:
#
####################################

DeclareRepresentation( "IsCapCategoryObjectInBialgebroidRep",
        IsCapCategoryObjectInBialgebroid and
        IsAttributeStoringRep,
        [ ] );

DeclareRepresentation( "IsCapCategoryMorphismInBialgebroidRep",
        IsCapCategoryMorphismInBialgebroid and
        IsAttributeStoringRep,
        [ ] );

####################################
#
# families and types:
#
####################################

# new families:
BindGlobal( "TheFamilyOfObjectsInBialgebroids",
        NewFamily( "TheFamilyOfObjectsInBialgebroids" ) );

BindGlobal( "TheFamilyOfMorphismsInBialgebroids",
        NewFamily( "TheFamilyOfMorphismsInBialgebroids" ) );

# new types:
BindGlobal( "TheTypeObjectInBialgebroid",
        NewType( TheFamilyOfObjectsInBialgebroids,
                IsCapCategoryObjectInBialgebroidRep ) );

BindGlobal( "TheTypeMorphismInBialgebroid",
        NewType( TheFamilyOfMorphismsInBialgebroids,
                IsCapCategoryMorphismInBialgebroidRep ) );

####################################
#
# methods for attributes:
#
####################################

####################################
#
# methods for operations:
#
####################################

##
InstallGlobalFunction( ADD_FUNCTIONS_FOR_ALGEBROID,
  
  function( category )
    
    ##
    AddIsWellDefinedForObjects( category,
      function( o )
        
        o := UnderlyingVertex( o );
        
        return IsVertex( o ) and IsIdenticalObj( QuiverOfPath( o ), UnderlyingQuiver( category ) );
        
      end );
    
    ##
    AddIsWellDefinedForMorphisms( category,
      function( m )
        
        m := UnderlyingPathAlgebraElement( m );
        
        return IsPath( m ) and IsIdenticalObj( QuiverOfPath( m ), UnderlyingQuiver( category ) );
        
      end );
    
    ##
    AddIsEqualForObjects( category,
      function( object_1, object_2 )
        
        return UnderlyingVertex( object_1 ) = UnderlyingVertex( object_2 );
        
    end );
    
    ##
    AddIsEqualForMorphisms( category,
      function( morphism_1, morphism_2 )
        
        return UnderlyingPathAlgebraElement( morphism_1 ) = UnderlyingPathAlgebraElement( morphism_2 );
        
    end );
    
    ##
    AddIdentityMorphism( category,
      function( object )
        local A, id;
        
        A := UnderlyingPathAlgebra( CapCategory( object ) );
        
        id := rec( );
        
        ObjectifyWithAttributes( id, TheTypeMorphismInBialgebroid,
                Source, object,
                Range, object,
                UnderlyingPathAlgebraElement, A.(String( UnderlyingVertex( object ) ) )
                );
        
        return id;
        
    end );
    
    ##
    AddPreCompose( category,
      function( morphism_1, morphism_2 )
        local B, quiver;
        
        B := CapCategory( morphism_1 );
        
        quiver := UnderlyingQuiver( B );
        
        if IsRightQuiver( quiver ) then
            return MorphismInBialgebroid(
                           Source( morphism_1 ),
                           UnderlyingPathAlgebraElement( morphism_1 ) * UnderlyingPathAlgebraElement( morphism_2 ),
                           Range( morphism_2 ) );
        else
            return MorphismInBialgebroid(
                           Source( morphism_1 ),
                           UnderlyingPathAlgebraElement( morphism_2 ) * UnderlyingPathAlgebraElement( morphism_1 ),
                           Range( morphism_2 ) );
        fi;
        
    end );
    
    ##
    AddAdditionForMorphisms( category,
      function( morphism_1, morphism_2 )
        
        return MorphismInBialgebroid(
                       Source( morphism_1 ),
                       UnderlyingPathAlgebraElement( morphism_1 ) + UnderlyingPathAlgebraElement( morphism_2 ),
                       Range( morphism_1 ) );
        
    end );
    
    ##
    AddAdditiveInverseForMorphisms( category,
      function( morphism )
        
        return MorphismInBialgebroid(
                       Source( morphism ),
                       -UnderlyingPathAlgebraElement( morphism ),
                       Range( morphism ) );
        
    end );
    
    ##
    AddZeroMorphism( category,
      function( S, T )
        
        return MorphismInBialgebroid(
                       S,
                       Zero( UnderlyingPathAlgebra( CapCategory( S ) ) ),
                       T );
        
    end );
    
    return category;
    
end );

##
InstallGlobalFunction( ADD_FUNCTIONS_FOR_BIALGEBROID,
  
  function( category )
    
    return category;
    
end );

##
InstallMethod( Algebroid_NonFinalized,
        "for a homalg ring and a QPA quiver",
        [ IsHomalgRing, IsQuiver ],
        
  function( R, quiver )
    local parity, A;
    
    if IsRightQuiver( quiver ) then
        parity := "right";
    else
        parity := "left";
    fi;
    
    A := Concatenation( "Algebroid generated by the ", parity, " quiver ", String( quiver ) );
    
    A := CreateCapCategory( A );
    
    SetUnderlyingQuiver( A, quiver );
    SetUnderlyingPathAlgebra( A, PathAlgebra( R, quiver ) );
    
    return ADD_FUNCTIONS_FOR_ALGEBROID( A );
    
end );

##
InstallMethod( Algebroid,
        "for a homalg ring and a QPA quiver",
        [ IsHomalgRing, IsQuiver ],
        
  function( R, quiver )
    local A;
    
    A := Algebroid_NonFinalized( R, quiver );
    
    Finalize( A );
    
    return A;
    
end );

##
InstallMethod( Bialgebroid,
        "for a homalg ring and a QPA quiver",
        [ IsHomalgRing, IsQuiver ],
        
  function( R, quiver )
    local B;
    
    B := Algebroid_NonFinalized( R, quiver );
    
    ADD_FUNCTIONS_FOR_BIALGEBROID( B );
    
    Finalize( B );
    
    return B;
    
end );

##
InstallMethod( MorphismInBialgebroid,
        "for two objects in a bialgebroid and an element of the path algebra",
        [ IsCapCategoryObjectInBialgebroidRep, IsPathAlgebraElement, IsCapCategoryObjectInBialgebroidRep ],
        
  function( S, path, T )
    local l, mor, B;
    
    if not IsZero( path ) then
        
        if not IsUniform( path ) then
            Error( "the path ", path, " is neither zero nor uniform\n" );
        fi;
        
        l := LeadingPath( path );
        
        if not ( Source( l ) = UnderlyingVertex( S ) and
                 Target( l ) = UnderlyingVertex( T ) ) then
            Error( "the path ", path, " is neither zero nor does it match the given source S or target T\n" );
        fi;
        
    fi;
    
    mor := rec( );
    
    B := CapCategory( S );
    
    ObjectifyWithAttributes( mor, TheTypeMorphismInBialgebroid,
            Source, S,
            Range, T,
            UnderlyingPathAlgebraElement, path
            );
    
    return mor;
    
end );

##
InstallMethod( \.,
        "for a bialgebroid and a positive integer",
        [ IsCapCategory, IsPosInt ],
        
  function( B, string_as_int )
    local q, name, a, b, A;
    
    q := UnderlyingQuiver( B );
    
    name := NameRNam( string_as_int );
    
    a := q.(name);
    
    b := rec( );
    
    if IsVertex( a ) then
        ObjectifyWithAttributes( b, TheTypeObjectInBialgebroid,
                UnderlyingVertex, a
                );
    elif IsArrow( a ) then
        A := UnderlyingPathAlgebra( B );
        ObjectifyWithAttributes( b, TheTypeMorphismInBialgebroid,
                Source, B.(String( Source( a ) ) ),
                Range, B.(String( Target( a ) ) ),
                UnderlyingPathAlgebraElement, PathAsAlgebraElement( A, a )
                );
    else
        Error( "the given component ", name, " is neither a vertex nor an arrow of the quiver q = ", q, "\n" );
    fi;
    
    Add( B, b );
    
    return b;
    
end );

####################################
#
# View, Print, and Display methods:
#
####################################

##
InstallMethod( ViewObj,
        "for an object in a bialgebroid",
        [ IsCapCategoryObjectInBialgebroidRep ],

  function( o )
    
    ViewObj( UnderlyingVertex( o ) );
    
end );

##
InstallMethod( ViewObj,
        "for a morphism in a bialgebroid",
        [ IsCapCategoryMorphismInBialgebroidRep ],

  function( o )
    
    if IsRightQuiverAlgebra( UnderlyingPathAlgebra( CapCategory( o ) ) ) then
        ViewObj( Source( o ) );
        Print( "-[" );
        ViewObj( UnderlyingPathAlgebraElement( o ) );
        Print( "]->" );
        ViewObj( Range( o ) );
    else
        ViewObj( Range( o ) );
        Print( "<-[" );
        ViewObj( UnderlyingPathAlgebraElement( o ) );
        Print( "]-" );
        ViewObj( Source( o ) );
    fi;
    
end );
