#
# Bialgebroids: Bialgebroids as preadditive categories generated by enhanced quivers
#
# Implementations
#

####################################
#
# representations:
#
####################################

DeclareRepresentation( "IsCapCategoryObjectInAlgebroidRep",
        IsCapCategoryObjectInAlgebroid and
        IsAttributeStoringRep,
        [ ] );

DeclareRepresentation( "IsCapCategoryMorphismInAlgebroidRep",
        IsCapCategoryMorphismInAlgebroid and
        IsAttributeStoringRep,
        [ ] );

####################################
#
# methods for attributes:
#
####################################

##
InstallMethod( SetOfObjects,
        "for an algebroid",
        [ IsAlgebroid and HasUnderlyingQuiver ],
        
  A -> List( Vertices( UnderlyingQuiver( A ) ), o -> A.(String( o ) ) ) );

##
InstallMethod( SetOfGeneratingMorphisms,
        "for an algebroid",
        [ IsAlgebroid and HasUnderlyingQuiver ],
        
  A -> List( Arrows( UnderlyingQuiver( A ) ), o -> A.(String( o ) ) ) );

##
InstallMethod( RelationsOfAlgebroid,
        "for an algebroid",
        [ IsAlgebroid and HasUnderlyingQuiverAlgebra ],
        
  function( A )
    local relations;
    
    relations := RelationsOfAlgebra( UnderlyingQuiverAlgebra( A ) );
    
    relations := Filtered( relations, r -> not IsZero( r ) );
    
    return List( relations, MorphismInAlgebroid );
    
end );

##
InstallMethod( BijectionBetweenPairsAndElementaryTensors,
        "for a QPA quiver algebra",
        [ IsQuiverAlgebra ],
        
  function( Qq )
    local Qqq, gens, prod;
    
    if HasPathAlgebra( Qq ) then
        Qq := PathAlgebra( Qq );
    fi;
    
    Qqq := TensorProductOfAlgebras( Qq, Qq );
    
    gens := PrimitivePaths( QuiverOfAlgebra( Qq ) );
    
    gens := Cartesian( gens, gens );
    
    prod := List( gens,
                  p -> ElementaryTensor(
                          PathAsAlgebraElement( Qq, p[1] ),
                          PathAsAlgebraElement( Qq, p[2] ),
                          Qqq ) );
    
    
    prod := List( prod, a -> Paths( Representative( a ) )[1] );
    
    return [ prod, gens ];
    
end );

##
InstallMethod( DecompositionOfMorphismInAlgebroid,
        "for a morphism in an algebroid",
        [ IsCapCategoryMorphismInAlgebroidRep ],
        
  function( mor )
    local B, func;
    
    B := CapCategory( mor );
    
    mor := UnderlyingQuiverAlgebraElement( mor );
    
    mor := DecomposeQuiverAlgebraElement( mor );
    
    func :=
      function( a )
        return List( a,
                     function( f )
                       f := B.(String( f ));
                       if IsCapCategoryObject( f ) then
                           return IdentityMorphism( f );
                       fi;
                       return f;
                     end );
        
      end;
    
    mor[2] := List( mor[2], func );
    
    return ListN( mor[1], mor[2], function( r, s ) return [ r, s ]; end );
    
end );

##
InstallMethod( DecompositionOfMorphismInSquareOfAlgebroid,
        "for a morphism in an algebroid",
        [ IsCapCategoryMorphismInAlgebroidRep ],
        
  function( mor )
    local B, Rq, gens, prod, func;
    
    B := CapCategory( mor )!.PowerOf;
    
    Rq := UnderlyingQuiverAlgebra( B );
    
    gens := BijectionBetweenPairsAndElementaryTensors( Rq );
    prod := gens[2];
    gens := gens[1];
    
    mor := UnderlyingQuiverAlgebraElement( mor );
    
    mor := DecomposeQuiverAlgebraElement( mor );
    
    mor[2] := List( mor[2], p -> List( p, a -> prod[Position( gens, a )] ) );
    
    func :=
      function( a )
        return List( a,
                     function( f )
                       f := B.(String( f ));
                       if IsCapCategoryObject( f ) then
                           return IdentityMorphism( f );
                       fi;
                       return f;
                     end );
        
      end;
    
    mor[2] := List( mor[2], p -> List( p, func ) );
    
    return ListN( mor[1], mor[2], function( r, s ) return [ r, s ]; end );
    
end );

####################################
#
# methods for operation:
#
####################################

##
InstallMethod( DecomposeQuiverAlgebraElement,
        "for a quiver algebra element",
        [ IsQuiverAlgebraElement ],
        
  function( p )
    
    if IsQuotientOfPathAlgebraElement( p ) then
        p := Representative( p );
    fi;
    
    return [ Coefficients( p ),
             List( Paths( p ),
                   function( a )
                     if Length( a ) = 0 then
                         return [ a ];
                     fi;
                     return ArrowList( a );
                   end ) ];
    
end );

##
InstallMethod( ApplyToQuiverAlgebraElement,
        "for an object function, a morphism function, a category, a quiver algebra element and a boolean",
        [ IsFunction, IsFunction, IsCapCategory, IsQuiverAlgebraElement, IsBool ],

    function( object_function, morphism_function, range_category, p, contravariant )
      local applyF, paths, coefs, paths_final, s, all_objects_in_image;
    
    # function to be applied to an arrow (or a vertex representing the trivial path at this vertex)
    applyF :=
      function( b )
        local m;
        
        if IsQuiverVertex( b ) then
            m := object_function( b );
            return IdentityMorphism( m );
        fi;
        m := morphism_function( b );
        
        return m;
        
      end;
    
    paths := DecomposeQuiverAlgebraElement( p );

    coefs := paths[1];
    paths := paths[2];
    
    if contravariant = false then
        paths_final := List( paths, a -> PreCompose( List( a, applyF ) ) );
    else
        paths_final := List( paths, a -> PreCompose( Reversed( List( a, applyF ) ) ) );
    fi;

    if Length( coefs ) > 0 then
      s := Sum( ListN( coefs, paths_final, function( r, p ) return r * p; end ) );
    else
      all_objects_in_image := SetOfObjects( range_category );
      s := Sum( List( all_objects_in_image, o -> ZeroMorphism(o,o)) );
    fi;
    
    return s;

end );

##
InstallMethod( ApplyToQuiverAlgebraElement,
        "for a record (of images of objects), a record (of images of morphisms) and a quiver algebra element",
        [ IsRecord, IsRecord, IsQuiverAlgebraElement, IsBool ],
  
  function( F_objects, F_morphisms, p, contravariant )
    local func_obj, func_mor, eval_F_objects, some_object_in_image;
    
    func_obj := b -> F_objects.(String( b ));
    func_mor := b -> F_morphisms.(String( b ));
      
    some_object_in_image := List( RecNames(F_objects), func_obj )[1];

    return ApplyToQuiverAlgebraElement( func_obj, func_mor, CapCategory(some_object_in_image), p, contravariant );
    
end );

##
InstallMethod( ApplyToQuiverAlgebraElement,
        "for a functor and a quiver algebra element",
        [ IsCapFunctor, IsQuiverAlgebraElement ],

  function( functor, p )
    local func, b;
    
    func := b -> ApplyFunctor( functor, AsCapCategory(Source(functor)).(String(b)) );
    
    if IsBound( functor!.IsContravariant ) and functor!.IsContravariant then
        return ApplyToQuiverAlgebraElement( func, func, AsCapCategory(Range(functor)), p, true );
    else
        return ApplyToQuiverAlgebraElement( func, func, AsCapCategory(Range(functor)), p, false );
    fi;
end );

####################################
#
# methods for constructors:
#
####################################

##
InstallGlobalFunction( ADD_FUNCTIONS_FOR_ALGEBROID,
  
  function( category, over_Z )
    
    ##
    AddIsWellDefinedForObjects( category,
      function( o )
        
        o := UnderlyingVertex( o );
        
        return IsQuiverVertex( o ) and IsIdenticalObj( QuiverOfPath( o ), UnderlyingQuiver( category ) );
        
      end );
    
    ##
    AddIsWellDefinedForMorphisms( category,
      function( alpha )
        local m, v, w;
        
        m := UnderlyingQuiverAlgebraElement( alpha );
        
        if not IsIdenticalObj( AlgebraOfElement( m ), UnderlyingQuiverAlgebra( category ) ) then
            
            return false;
            
        fi;
        
        if IsZero( m ) then
            
            return true;
            
        fi;
        
        if not IsUniform( m ) then
            
            return false;
            
        fi;
        
        if IsQuotientOfPathAlgebraElement( m ) then
            
            m := Representative( m );
            
        fi;
        
        v := Source( LeadingPath( m ) ); 
        
        if not ( UnderlyingVertex( Source( alpha ) ) = v ) then
            
            return false;
            
        fi;
        
        w := Target( LeadingPath( m ) );
        
        if not ( UnderlyingVertex( Range( alpha ) ) = w ) then
            
            return false;
            
        fi;
        
        # all tests passed, so it is well-defined
        return true;
        
      end );
    
    ##
    AddIsEqualForObjects( category,
      function( object_1, object_2 )
        
        return UnderlyingVertex( object_1 ) = UnderlyingVertex( object_2 );
        
    end );
    
    ##
    AddIsEqualForMorphisms( category,
      function( morphism_1, morphism_2 )
        
        return UnderlyingQuiverAlgebraElement( morphism_1 ) = UnderlyingQuiverAlgebraElement( morphism_2 );
        
    end );
    
    ##
    AddIdentityMorphism( category,
      function( object )
        local quiver_algebra, id;
        
        quiver_algebra := UnderlyingQuiverAlgebra( CapCategory( object ) );
        
        id := PathAsAlgebraElement( quiver_algebra, UnderlyingVertex( object ) );
        
        return MorphismInAlgebroid(
                       object,
                       id,
                       object );
        
    end );
    
    ##
    AddPreCompose( category,
      function( morphism_1, morphism_2 )
        local B, quiver;
        
        B := CapCategory( morphism_1 );
        
        quiver := UnderlyingQuiver( B );
        
        if IsRightQuiver( quiver ) then
            return MorphismInAlgebroid(
                           Source( morphism_1 ),
                           UnderlyingQuiverAlgebraElement( morphism_1 ) * UnderlyingQuiverAlgebraElement( morphism_2 ),
                           Range( morphism_2 ) );
        else
            return MorphismInAlgebroid(
                           Source( morphism_1 ),
                           UnderlyingQuiverAlgebraElement( morphism_2 ) * UnderlyingQuiverAlgebraElement( morphism_1 ),
                           Range( morphism_2 ) );
        fi;
        
    end );
    
    ##
    AddAdditionForMorphisms( category,
      function( morphism_1, morphism_2 )
        
        return MorphismInAlgebroid(
                       Source( morphism_1 ),
                       UnderlyingQuiverAlgebraElement( morphism_1 ) + UnderlyingQuiverAlgebraElement( morphism_2 ),
                       Range( morphism_1 ) );
        
    end );
    
    ##
    AddAdditiveInverseForMorphisms( category,
      function( morphism )
        
        return MorphismInAlgebroid(
                       Source( morphism ),
                       -UnderlyingQuiverAlgebraElement( morphism ),
                       Range( morphism ) );
        
    end );
    
    #
    AddMultiplyWithElementOfCommutativeRingForMorphisms( category,
      function( r, morphism )
        
        return MorphismInAlgebroid(
                       Source( morphism ),
                       r * UnderlyingQuiverAlgebraElement( morphism ),
                       Range( morphism ) );
        
    end );
    
    ##
    AddZeroMorphism( category,
      function( S, T )
        
        return MorphismInAlgebroid(
                       S,
                       Zero( UnderlyingQuiverAlgebra( CapCategory( S ) ) ),
                       T );
        
    end );
    
    ## only create the Hom-structure for finite dimensional quiver algebras
    if IsFiniteDimensional( UnderlyingQuiverAlgebra( category ) ) then
        
        ADD_FUNCTIONS_FOR_HOM_STRUCTURE_OF_ALGEBROID( category, over_Z );
        
    fi;
    
    Finalize( category );
    SetFilterObj(IdentityFunctor(category), IsAlgebroidMorphism);
    
    return category;
    
end );

InstallGlobalFunction( ADD_FUNCTIONS_FOR_HOM_STRUCTURE_OF_ALGEBROID,
    function( bialgebroid, over_Z )
    local quiver_algebra, quiver, vertices, basis, data, path,
          object_constructor, morphism_constructor, distinguished_object, ring, range_category;
    
    quiver_algebra := UnderlyingQuiverAlgebra( bialgebroid );
    
    ## Prepare quick access to basis elements
    quiver := QuiverOfAlgebra( quiver_algebra );
    
    vertices := Vertices( quiver );
    
    basis := BasisPaths( CanonicalBasis( quiver_algebra ) );
    
    data := List( vertices, i -> List( vertices, i -> [ ] ) );
    
    for path in basis do
        
        Add( data[ VertexNumber( Source( path ) ) ][ VertexNumber( Target( path ) ) ], path );
        
    od;
    
    ring := CommutativeRingOfLinearCategory( bialgebroid );
    
    if over_Z then
        
        range_category := CategoryOfRows( ring );
        
        object_constructor := function( size )
        return CategoryOfRowsObject( size, range_category );
        end;
        
        morphism_constructor := AsCategoryOfRowsMorphism;
        
        distinguished_object := function()
        return CategoryOfRowsObject( 1, range_category );
        end;
    
    else
        
        object_constructor := function( size )
        return VectorSpaceObject( size, ring );
        end;
        
        morphism_constructor := function( mat )
        return VectorSpaceMorphism(
                VectorSpaceObject( NrRows( mat ), ring ),
                mat,
                VectorSpaceObject( NrColumns( mat ), ring )
                );
        end;
        
        range_category := MatrixCategory( ring );
        
        distinguished_object := function()
        return TensorUnit( range_category );
        end;
        
    fi;
    
    SetRangeCategoryOfHomomorphismStructure( bialgebroid, range_category );
    
    ##
    AddHomomorphismStructureOnObjects( bialgebroid,
      function( object_1, object_2 )
        local nr_source, nr_range, basis_elements;
        
        nr_source := VertexNumber( UnderlyingVertex( object_1 ) );
        
        nr_range := VertexNumber( UnderlyingVertex( object_2 ) );
        
        basis_elements := data[nr_source][nr_range];
        
        return object_constructor( Size( basis_elements ) );
        
    end );
    
    ##
    AddHomomorphismStructureOnMorphismsWithGivenObjects( bialgebroid,
      function( source, alpha, beta, range )
        local elem_alpha, elem_beta, a, b, ap, bp, basis_elements_source, basis_elements_range, size_source, size_range, images, path;
        
        elem_alpha := UnderlyingQuiverAlgebraElement( alpha );
        
        elem_beta := UnderlyingQuiverAlgebraElement( beta );
        
        a := VertexNumber( UnderlyingVertex( Range( alpha ) ) );
        
        b := VertexNumber( UnderlyingVertex( Source( beta ) ) );
        
        ap := VertexNumber( UnderlyingVertex( Source( alpha ) ) );
        
        bp := VertexNumber( UnderlyingVertex( Range( beta ) ) );
        
        basis_elements_source := data[a][b];
        
        basis_elements_range := data[ap][bp];
        
        size_source := Size( basis_elements_source );
        
        size_range := Size( basis_elements_range );
        
        if size_source = 0 or size_range = 0 then
            
            return morphism_constructor( HomalgZeroMatrix( size_source, size_range, ring ) );
            
        fi;
        
        images := [ ];
        
        if IsQuotientOfPathAlgebraElement( elem_alpha ) then
            
            for path in basis_elements_source do
                
                Add( images,
                  CoefficientsOfPaths( basis_elements_range, Representative( elem_alpha * PathAsAlgebraElement( quiver_algebra, path ) * elem_beta ) )
                );
                
            od;
            
        else
        
            for path in basis_elements_source do
                
                Add( images,
                  CoefficientsOfPaths( basis_elements_range, ( elem_alpha * PathAsAlgebraElement( quiver_algebra, path ) * elem_beta ) )
                );
                
            od;
            
        fi;
        
        return morphism_constructor( HomalgMatrix( images, size_source, size_range, ring ) );
        
    end );
    
    ##
    AddDistinguishedObjectOfHomomorphismStructure( bialgebroid,
      function( )
        
        return distinguished_object();
        
    end );
    
    ##
    AddInterpretMorphismAsMorphismFromDinstinguishedObjectToHomomorphismStructure( bialgebroid,
      function( alpha )
        local a, b, basis_elements, size_basis, element;
        
        a := VertexNumber( UnderlyingVertex( Source( alpha ) ) );
        
        b := VertexNumber( UnderlyingVertex( Range( alpha ) ) );
        
        basis_elements := data[a][b];
        
        size_basis := Size( basis_elements );
        
        if size_basis = 0 then
            
            return morphism_constructor( HomalgZeroMatrix( 1, 0, ring ) );
            
        fi;
        
        element := UnderlyingQuiverAlgebraElement( alpha );
        
        if IsQuotientOfPathAlgebraElement( element ) then
            
            return morphism_constructor(
                    HomalgMatrix( CoefficientsOfPaths( basis_elements, Representative( element ) ), 1, size_basis, ring )
                  );
            
        else
            
            return morphism_constructor(
                    HomalgMatrix( CoefficientsOfPaths( basis_elements, element ), 1, size_basis, ring )
                  );
            
        fi;
        
    end );
    
    ##
    AddInterpretMorphismFromDinstinguishedObjectToHomomorphismStructureAsMorphism( bialgebroid,
      function( a, b, morphism )
        local coefficients, basis, element;
        
        coefficients := EntriesOfHomalgMatrix( UnderlyingMatrix( morphism ) );
        
        basis := data[VertexNumber( UnderlyingVertex( a ) )][VertexNumber( UnderlyingVertex( b ) )];
        
        element := QuiverAlgebraElement( quiver_algebra, coefficients, basis );
        
        return MorphismInAlgebroid( a, element, b );
        
    end );
    
end );

##
InstallMethod( Algebroid,
        "for a QPA quiver algebra",
        [ IsQuiverAlgebra ],
    function( A )
        return Algebroid( A, false );
end );

##
InstallMethod( DescentToZDefinedByBasisPaths,
        "for an algebroid",
        [ IsAlgebroid ],
    function( A )
        return Algebroid( UnderlyingQuiverAlgebra( A ), true );
end );

##
InstallMethod( Algebroid,
        "for a QPA quiver algebra and a boolean",
        [ IsQuiverAlgebra, IsBool ],
        
  function( Rq, over_Z )
    local domain, is_int_test, homalg_rationals, paths, p, q, parity, quiver, A;
    
    domain := LeftActingDomain( Rq );
    
    if over_Z then
        
        if IsIdenticalObj( domain, Rationals )
           or
           ( HasIsRationalsForHomalg( domain ) and IsRationalsForHomalg( domain )
             and not IsHomalgExternalRingRep( domain ) )
        then
            
            ## Test if the multiplication table which is induced by the basis of the quiver algebra
            ## (computed by the Groebner basis) only has integral coefficients.
            paths := BasisPaths( CanonicalBasis( Rq ) );
            
            for p in paths do
                
                for q in paths do
                    
                    if not ForAll( Coefficients( PathAsAlgebraElement( Rq, p ) * PathAsAlgebraElement( Rq, q ) ), IsInt ) then
                        
                        Error( "the basis of the given quiver algebra does give rise to a well-defined algebroid over Z\n");
                        
                    fi;
                    
                od;
                
            od;
            
        else
            
            Error( "the left acting domain of the given quiver algebra is not supported for constructing an algebroid of the integers\n");
            
        fi;
        
    fi;
    
    if IsRightQuiverAlgebra( Rq ) then
        parity := "right";
    else
        parity := "left";
    fi;
    
    quiver := QuiverOfAlgebra( Rq );
    
    if Length( Vertices( quiver ) ) > 1 then
        A := "Algebroid";
    else
        A := "Algebra";
    fi;
    
    if over_Z then
        
        A := Concatenation( A, " over Z");
        
    fi;
    
    A := Concatenation( A, " generated by the ", parity, " quiver ", String( quiver ) );
    
    A := CreateCapCategory( A );
    
    SetIsAbCategory( A, true );
    
    SetIsFinitelyPresentedCategory( A, true );
    SetUnderlyingQuiver( A, quiver );
    
    if over_Z then
        SetCommutativeRingOfLinearCategory( A, HomalgRingOfIntegers() );
    else
        SetCommutativeRingOfLinearCategory( A, LeftActingDomain( Rq ) );
    fi;
    
    SetUnderlyingQuiverAlgebra( A, Rq );
    SetFilterObj( A, IsAlgebroid );
    if Length( Vertices( quiver ) ) = 1 then
        SetFilterObj( A, IsAlgebraAsCategory );
    fi;
    
    AddObjectRepresentation( A, IsCapCategoryObjectInAlgebroidRep );
    AddMorphismRepresentation( A, IsCapCategoryMorphismInAlgebroidRep );
    
    A!.Vertices := rec( );
    A!.Arrows := rec( );
    
    return ADD_FUNCTIONS_FOR_ALGEBROID( A, over_Z );
    
end );

##
InstallMethod( Algebroid,
        "for a QPA path algebra and a list",
        [ IsPathAlgebra, IsList ],
        
  function( Rq, L )
    local path;
    
    for path in L do
        if not IsUniform( path ) then
            Error( "only uniform relations are admissible, while the path ", path, " is not uniform\n" );
        fi;
    od;
    
    L := Filtered( L, r -> not IsZero( r ) );
    
    return Algebroid( Rq / Ideal( Rq, L ) );
    
end );

##
InstallMethod( Algebroid,
        "for a homalg ring and a QPA quiver",
        [ IsHomalgRing, IsQuiver ],
        
  function( R, quiver )
    
    return Algebroid( PathAlgebra( R, quiver ) );
    
end );


##
InstallMethod( Multiplication,
        "for a commutative algebra as cateogry",
        [ IsAlgebraAsCategory and IsCommutative ],
  function (A)
    local unique_object_in_A, unique_object_in_A_string, morphisms_in_A, A2, unique_object_in_A2, unique_object_in_A2_string, mult_functor_images_of_objects, mult_functor_images_of_morphisms, a, b, axg, axg_string, fxb, fxb_string, mult_functor, g, f;

    unique_object_in_A := SetOfObjects( A )[1];
    unique_object_in_A_string := String( UnderlyingVertex( unique_object_in_A ) );
    morphisms_in_A := SetOfGeneratingMorphisms(A);

    A2 := A*A;
    
    unique_object_in_A2 := SetOfObjects( A2 )[1];
    unique_object_in_A2_string := String( UnderlyingVertex( unique_object_in_A2 ) );
    
    mult_functor_images_of_objects := rec( );
    mult_functor_images_of_objects.(unique_object_in_A2_string) := unique_object_in_A;

    mult_functor_images_of_morphisms := rec(); # FIXME

    a := unique_object_in_A;
    b := unique_object_in_A;

    for g in morphisms_in_A do

        axg := ElementaryTensor( a, g, A2 );
        axg_string := String( LeadingPath( Representative( UnderlyingQuiverAlgebraElement( axg ) ) ) );

        mult_functor_images_of_morphisms.(axg_string) := g;

    od;

    for f in morphisms_in_A do

        fxb := ElementaryTensor( f, b, A2 );
        fxb_string := String( LeadingPath( Representative( UnderlyingQuiverAlgebraElement( fxb ) ) ) );

        mult_functor_images_of_morphisms.(fxb_string) := f;

    od;
    
    mult_functor := CapFunctor( A2, mult_functor_images_of_objects, mult_functor_images_of_morphisms );
    
    return mult_functor;

end );

##
InstallMethod( Unit,
        "for algebra as category",
        [ IsAlgebraAsCategory ],
  function( A )
    local unique_object_in_A, unique_object_in_A_string, unique_object_in_A0, unique_object_in_A0_string, unit_functor_images_of_objects, unit_functor_images_of_morphisms, A0;
    
    unique_object_in_A := SetOfObjects( A )[1];
    unique_object_in_A_string := String( UnderlyingVertex( unique_object_in_A ) );
    
    A0 := A^0;
    
    unique_object_in_A0 := SetOfObjects( A0 )[1];
    unique_object_in_A0_string := String( UnderlyingVertex( unique_object_in_A0 ) );
    
    unit_functor_images_of_objects := rec( );
    unit_functor_images_of_objects.(unique_object_in_A0_string) := unique_object_in_A;
    unit_functor_images_of_morphisms := rec( );
    
    return CapFunctor(A0, unit_functor_images_of_objects, unit_functor_images_of_morphisms );
end );
##
InstallMethod( \.,
        "for an algebroid and a positive integer",
        [ IsAlgebroid, IsPosInt ],
        
  function( B, string_as_int )
    local q, name, a, b;
    
    q := UnderlyingQuiver( B );
    
    name := NameRNam( string_as_int );
    
    a := q.(name);
    
    b := rec( );
    
    if IsQuiverVertex( a ) then
        if IsBound( B!.Vertices.(name) ) then
            return B!.Vertices.(name);
        fi;
        ObjectifyObjectForCAPWithAttributes(
            b, B,
            UnderlyingVertex, a
        );
        B!.Vertices.(name) := b;
    elif IsArrow( a ) then
        if IsBound( B!.Arrows.(name) ) then
            return B!.Arrows.(name);
        fi;
        b := MorphismInAlgebroid(
                     B.(String( Source( a ) ) ),
                     PathAsAlgebraElement( UnderlyingQuiverAlgebra( B ), a ),
                     B.(String( Target( a ) ) ) );
        B!.Arrows.(name) := b;
    else
        Error( "the given component ", name, " is neither a vertex nor an arrow of the quiver q = ", q, "\n" );
    fi;
    
    return b;
     
end );

InstallMethod( ObjectInAlgebroid,
         "for an algebroid and a vertex of a quiver",
        [ IsAlgebroid, IsQuiverVertex ],
  function( A, v )
    local o;
    o := rec();
    ObjectifyObjectForCAPWithAttributes(
        o, A,
        UnderlyingVertex, v
    );
    A!.Vertices.(String(v)) := o;
    return o;
end );

##
InstallMethod( MorphismInAlgebroid,
        "for two objects in an algebroid and an element of the quiver algebra",
        [ IsCapCategoryObjectInAlgebroidRep, IsQuiverAlgebraElement, IsCapCategoryObjectInAlgebroidRep ],
        
  function( S, path, T )
    local l, mor, A;
    
    if not IsZero( path ) then
        
        if not IsUniform( path ) then
            Error( "the path ", path, " is neither zero nor uniform\n" );
        fi;
        
        ## TODO: we are avoiding for the moment the sanity test for
        ## elements of path algebras with relations, this should be
        ## reintroduced in the future
        if IsPathAlgebraElement( path ) then
            l := LeadingPath( path );
        
            if not ( Source( l ) = UnderlyingVertex( S ) ) and not ( Target( l ) = UnderlyingVertex( T ) ) then
                Error( "the path ", path, " is not zero, it does not match the given source S and it does not match the given target T\n" );
            elif not ( Source( l ) = UnderlyingVertex( S ) ) then
                Error( "the path ", path, " is not zero and it does not match the given source S\n" );
            elif not ( Target( l ) = UnderlyingVertex( T ) ) then
                Error( "the path ", path, " is not zero and it does not match the given target T\n" );
            fi;
        fi;
        
    fi;
    
    mor := rec( );
    
    if not IsIdenticalObj( CapCategory(S), CapCategory(T) ) then
        Error( "source and target do not belong to the same category");
    fi;

    A := CapCategory( S );
    
    ObjectifyMorphismForCAPWithAttributes(
            mor, A,
            Source, S,
            Range, T,
            UnderlyingQuiverAlgebraElement, path
            );
    
    return mor;
    
end );

##
InstallMethod( MorphismInAlgebroid,
        "an element of a path algebra",
        [ IsPathAlgebraElement ],
        
  function( path )
    local A, l, S, T;
    
    if IsZero( path ) then
        Error( "source and target of the zero path is ambiguous\n" );
    elif not IsUniform( path ) then
        Error( "the path ", path, " is not uniform\n" );
    fi;
    
    A := Algebroid( AlgebraOfElement( path ) );
    
    l := LeadingPath( path );
    
    S := String( Source( l ) );
    T := String( Target( l ) );
    
    return MorphismInAlgebroid( A.(S), path, A.(T) );
    
end );

##
InstallMethod( MorphismInAlgebroid,
        "an element of a quotient of a path algebra",
        [ IsQuotientOfPathAlgebraElement],
        
  function( path )
    local A, l, S, T;
    
    if IsZero( path ) then
        Error( "source and target of the zero path is ambiguous\n" );
    elif not IsUniform( path ) then
        Error( "the path ", path, " is not uniform\n" );
    fi;
    
    A := Algebroid( AlgebraOfElement( path ) );
    l := LeadingPath( path );
    
    S := String( Source( l ) );
    T := String( Target( l ) );
    
    return MorphismInAlgebroid( A.(S), path, A.(T) );

end );

##
InstallMethod( POW,
        "for an algebroid and an integer",
        [ IsAlgebroid and HasUnderlyingQuiverAlgebra, IsInt ],
        
  function( A, n )
    local Rq, R, trivial_quiver, Rqq;
    
    if n < 0 then
        Error( "the only admissible values for n are non-negative integers\n" );
    elif n = 1 then
        return A;
    elif not IsBound( A!.powers ) then
        A!.powers := rec( );
    fi;
    
    Rq := UnderlyingQuiverAlgebra( A );
    
    R := LeftActingDomain( Rq );
    
    if n = 0 then
        
        if not IsBound( A!.powers.0 ) then
            
            if IsRightQuiverAlgebra( Rq ) then
                trivial_quiver := RightQuiver( "*(1)[]" );
            else
                trivial_quiver := LeftQuiver( "*(1)[]" );
            fi;
            
            A!.powers.0 := Algebroid( PathAlgebra( R, trivial_quiver ) );
            
        fi;
        
        return A!.powers.0;
        
    fi;
    
    A!.powers.1 := A;
    
    if not IsBound( A!.powers.(n) ) then
        if not IsBound( A!.powers.(n-1) ) then
            A!.powers.(n-1) := A^(n-1);
        fi;
        
        A!.powers.(n) := A!.powers.(n-1) * A;
        
        A!.powers.(n)!.PowerOf := A;
        
    fi;
    
    return A!.powers.(n);
    
end );

##
InstallMethod(ElementaryTensor,
        "for objects in algebroids",
        [ IsCapCategoryObjectInAlgebroid, IsCapCategoryObjectInAlgebroid, IsAlgebroid ],
  function( a, b, T )
      
    local product_string, a_string, b_string, product_vertex;
  
    product_vertex := PathInProductQuiver( QuiverOfAlgebra( UnderlyingQuiverAlgebra( T ) ), [ UnderlyingVertex( a ), UnderlyingVertex( b ) ] );
    product_string := String(product_vertex);
  
    return T.(product_string);
end);

##
InstallMethod(ElementaryTensor,
        "for object and morphism in algebroids",
        [ IsCapCategoryObjectInAlgebroid, IsCapCategoryMorphismInAlgebroid, IsAlgebroid ],
  function( object, morphism, T )
    local product_string, coeffs, paths, object_string, sum, i, source, range, morphism_as_quiver_algebra_element, object_underlying_vertex, mors, algebroid_of_morphism, o, product_of_arrows_as_morphism, b, arrows_of_path;

    source := ElementaryTensor( object, Source(morphism), T );
    range := ElementaryTensor( object, Range(morphism), T );

    morphism_as_quiver_algebra_element := UnderlyingQuiverAlgebraElement( morphism );
    
    if IsQuotientOfPathAlgebraElement( morphism_as_quiver_algebra_element ) then
    
        morphism_as_quiver_algebra_element := Representative( morphism_as_quiver_algebra_element );
    
    fi;
    
    if IsZero(morphism_as_quiver_algebra_element) then
      
        return ZeroMorphism( source, range );

    else

        paths := Paths( morphism_as_quiver_algebra_element );
        coeffs := Coefficients( morphism_as_quiver_algebra_element );

        object_underlying_vertex := UnderlyingVertex( object );
        object_string := String( object_underlying_vertex );
       
        sum := ZeroMorphism( source, range );
        
        for i in [1..Length(paths)] do
          
            arrows_of_path := ArrowList( paths[i] ) ;
            
            mors := List( List( arrows_of_path, b -> PathInProductQuiver( QuiverOfAlgebra( UnderlyingQuiverAlgebra( T ) ), [ object_underlying_vertex, b ] ) ), b -> MorphismInAlgebroid( ObjectInAlgebroid( T, Source( b ) ), QuiverAlgebraElement( UnderlyingQuiverAlgebra( T ), [ 1 ], [ b ] ), ObjectInAlgebroid( T, Target( b ) ) ) );
            
            if Length( mors ) > 0 then
              
                product_of_arrows_as_morphism := PreCompose( mors );
              
            else
              
                Assert( 3, IsQuiverVertex( paths[i] ) );
                
                algebroid_of_morphism := CapCategory( morphism );
                
                o := ObjectInAlgebroid( algebroid_of_morphism, paths[i] );
                
                Assert( 3, o = Source( morphism ) and o = Range( morphism ) );
                
                product_of_arrows_as_morphism := IdentityMorphism( ElementaryTensor( object, o, T ) );
              
            fi;
            
            sum := sum + coeffs[i] * product_of_arrows_as_morphism;
          
        od;
        
        return sum;
      
    fi;
end );

##
InstallMethod(ElementaryTensor,
        "for morphism and object in algebroids",
        [ IsCapCategoryMorphismInAlgebroid, IsCapCategoryObjectInAlgebroid, IsAlgebroid ],
  function( morphism, object, T )
    local product_string, morphism_as_quiver_algebra_element, coeffs, paths, object_string, sum, i, source, range, object_underlying_vertex, mors, o, algebroid_of_morphism, product_of_arrows_as_morphism, arrows_of_path;

    source := ElementaryTensor( Source( morphism ), object, T );
    range := ElementaryTensor( Range( morphism ), object, T );
    
    morphism_as_quiver_algebra_element := UnderlyingQuiverAlgebraElement( morphism );

    if IsQuotientOfPathAlgebraElement( morphism_as_quiver_algebra_element ) then

        morphism_as_quiver_algebra_element := Representative( morphism_as_quiver_algebra_element );

    fi;

    if IsZero( morphism_as_quiver_algebra_element ) then

        return ZeroMorphism( source, range );

    else

        paths := Paths( morphism_as_quiver_algebra_element );
        coeffs := Coefficients( morphism_as_quiver_algebra_element );

        object_underlying_vertex := UnderlyingVertex( object );
        object_string := String( object_underlying_vertex );
       
        sum := ZeroMorphism( source, range );

        for i in [1..Length( paths )] do

            arrows_of_path := ArrowList( paths[ i ] );
                    
            mors := List( List( arrows_of_path, a -> PathInProductQuiver( QuiverOfAlgebra( UnderlyingQuiverAlgebra( T ) ), [ a, object_underlying_vertex ] ) ), a -> MorphismInAlgebroid( ObjectInAlgebroid( T, Source(a) ), QuiverAlgebraElement(UnderlyingQuiverAlgebra(T), [1], [a]), ObjectInAlgebroid(T, Target(a)) ) );

            if Length(mors) > 0 then

                product_of_arrows_as_morphism := PreCompose( mors );

            else

                Assert( 3, IsQuiverVertex( paths[i] ) );

                algebroid_of_morphism := CapCategory( morphism );

                o := ObjectInAlgebroid( algebroid_of_morphism, paths[i] );

                Assert( 3, o = Source( morphism ) and o = Range( morphism ) );

                product_of_arrows_as_morphism := IdentityMorphism( ElementaryTensor( o, object, T ) );
       
            fi;

            sum := sum + coeffs[i] * product_of_arrows_as_morphism;
            
        od;
        
        return sum;

    fi;
end );

##
InstallMethod(TensorProductOnObjects,
        "for algebroids",
        [ IsAlgebroid, IsAlgebroid ],
  function( X, Y )

    return X * Y;

end );

##
InstallMethod(TensorProductOnMorphisms,
        "for morphisms between algebroids",
        [ IsAlgebroidMorphism, IsAlgebroidMorphism ],
  function( F, G )
    local SF, SG, TF, TG, SF_objects, SG_objects, SFxSG, SFxSG_quiver_algebra, images_of_SF_objects_under_F, images_of_SG_objects_under_G, TFxTG, record_with_images_of_objects, a, b, axb, Gb, Fa, SF_mor, SG_mor, SF_mor_underlying_quiver_algebra_elements, SG_mor_underlying_quiver_algebra_elements, SF_mor_underlying_paths, SG_mor_underlying_paths, record_with_images_of_morphisms, g, axg, f, TF_objects, TG_objects, l, fxb, Ts, product_of_sources, product_of_targets, objects_of_product_of_targets, objects_of_product_of_sources, s, t, p, rp, lp, products_of_quiver_algebra_elements_of_generating_morphisms_of_sources, x;

    SF := AsCapCategory( Source( F ) );
    SG := AsCapCategory( Source( G ) );

    TF := AsCapCategory( Range( F ) );
    TG := AsCapCategory( Range( G ) );

    SF_objects := SetOfObjects( SF );
    SG_objects := SetOfObjects( SG );

    images_of_SF_objects_under_F := List( SF_objects, obj -> ApplyFunctor( F, obj ) );
    images_of_SG_objects_under_G := List( SG_objects, obj -> ApplyFunctor( G, obj ) );
    
    SFxSG := SF * SG;
    TFxTG := TF * TG;
    
    SFxSG_quiver_algebra := UnderlyingQuiverAlgebra( SFxSG );
    
    record_with_images_of_objects := rec();
    for a in SF_objects do
      for b in SG_objects do

        axb := ElementaryTensor( a, b, SFxSG );

        Fa := ApplyFunctor( F, a );
        Gb := ApplyFunctor( G, b );

        record_with_images_of_objects.( String( UnderlyingVertex( axb ) ) ) := ElementaryTensor( Fa, Gb, TFxTG );

      od;
    od;

    SF_mor := SetOfGeneratingMorphisms( SF );
    SG_mor := SetOfGeneratingMorphisms( SG );

    SF_mor_underlying_quiver_algebra_elements := List( SF_mor, UnderlyingQuiverAlgebraElement );
    SG_mor_underlying_quiver_algebra_elements := List( SG_mor, UnderlyingQuiverAlgebraElement );
    
    record_with_images_of_morphisms := rec();

    for a in SF_objects do
      for g in SG_mor do

        axg := ElementaryTensor( a, g, SFxSG );
        axg := UnderlyingQuiverAlgebraElement( axg );
        
        if not IsPathAlgebraElement( axg ) then
        
          axg := Representative( axg );

        fi;
        
        axg := String( LeadingPath( axg ) );

        record_with_images_of_morphisms.(axg) := ElementaryTensor( ApplyFunctor( F, a ), ApplyFunctor( G, g ), TFxTG );

      od;
    od;

    for f in SF_mor do
      for b in SG_objects do

        fxb := ElementaryTensor( f, b, SFxSG );
        fxb := UnderlyingQuiverAlgebraElement( fxb );

        if not IsPathAlgebraElement( fxb ) then

          fxb := Representative( fxb );

        fi;

        fxb := String( LeadingPath( fxb ) );

        record_with_images_of_morphisms.(fxb) := ElementaryTensor( ApplyFunctor( F, f ), ApplyFunctor( G, b ), TFxTG );

      od;
    od;

    TF_objects := SetOfObjects( TF );
    TG_objects := SetOfObjects( TG );

    l := Length( SF_mor );
    Ts := List( [1..l], x -> SFxSG_quiver_algebra );
    
    product_of_sources := SF*SG;
    product_of_targets := TF*TG;

    objects_of_product_of_sources := SetOfObjects( product_of_sources );
    objects_of_product_of_targets := SetOfObjects( product_of_targets );

    products_of_quiver_algebra_elements_of_generating_morphisms_of_sources := ListX( SF_mor_underlying_quiver_algebra_elements, SG_mor_underlying_quiver_algebra_elements, Ts, ElementaryTensor );

    return CapFunctor( SFxSG, record_with_images_of_objects, record_with_images_of_morphisms );

end );

##
InstallMethod( \*,
        "for two algebroids",
        [ IsAlgebroid and HasUnderlyingQuiverAlgebra, IsAlgebroid and HasUnderlyingQuiverAlgebra ],
        
  function( A, B )
    return Algebroid( TensorProductOfAlgebras( UnderlyingQuiverAlgebra( A ), UnderlyingQuiverAlgebra( B ) ) );
end );

##
InstallMethod( CapFunctor,
        "for an algebroid and two records",
        [ IsAlgebroid, IsRecord, IsRecord ],
        
  function( A, ImagesOfObjects, ImagesOfMorphisms )
    local b, Rq, B, functor, names_morphisms, names_objects;
    
    names_morphisms := NamesOfComponents( ImagesOfMorphisms );
    names_objects := NamesOfComponents( ImagesOfObjects );
    
    if names_objects = [ ] then
        Error( "the record of images of objects is empty\n" );
    fi;

    # Construct the target category B
    if Length(names_morphisms) > 0 then
        for b in names_morphisms do
            if IsQuiverAlgebraElement( ImagesOfMorphisms.(b) ) then
                Rq := AlgebraOfElement( ImagesOfMorphisms.(b) );
                B := Algebroid( Rq );
                break;
            elif IsCapCategoryCell( ImagesOfMorphisms.(b) ) then
                if not IsCapCategoryMorphism( ImagesOfMorphisms.(b) ) then
                    Error( Concatenation( "image of ", String(b), " is not a morphism"));
                fi;

                B := CapCategory( ImagesOfMorphisms.(b) );
                break;
            fi;
        od;
    else
        B := CapCategory( ImagesOfObjects.(names_objects[1]) );
    fi;

    if not IsBound( B ) then
        Error( "unable to extract target category from the records of images\n" );
    fi;
    
    functor := Concatenation( "Functor from finitely presented ", Name( A ), " -> ", Name( B ) );
   
    functor := CapFunctor( functor, A, B );
    
    AddObjectFunction( functor,
            obj -> ImagesOfObjects.(String( UnderlyingVertex( obj ) )) );
    
    AddMorphismFunction( functor,
            function( new_source, mor, new_range )
              if IsBound( functor!.IsContravariant ) and functor!.IsContravariant then
                  return ApplyToQuiverAlgebraElement( ImagesOfObjects, ImagesOfMorphisms, UnderlyingQuiverAlgebraElement( mor ), true);
              else
                  return ApplyToQuiverAlgebraElement( ImagesOfObjects, ImagesOfMorphisms, UnderlyingQuiverAlgebraElement( mor ), false );
              fi;
            end );
    
    SetFilterObj( functor, IsAlgebroidMorphism );
    
    return functor;
    
end );

##
InstallMethod( AddBialgebroidStructure,
        "for an algebroid and two records",
        [ IsAlgebroid, IsRecord, IsRecord ],
        
  function( B, counit_images, comult_images )
    local vertices, B0, counit_record_morphisms, counit_record_objects, counit_functor, B2, comult_record_morphisms, comult_record_objects, comult_functor, a;
    
    B!.Name := Concatenation( "Bia", B!.Name{[ 2 .. Length( B!.Name ) ]} );
    
    vertices := List( Vertices( UnderlyingQuiver( B ) ), String );
    
    B0 := B^0;
    
    # Construct the counit as a CapFunctor from B to B^0
    
    counit_record_morphisms := ShallowCopy( counit_images );
    counit_record_objects := rec();
  
    for a in NamesOfComponents( counit_images ) do
        if not IsCapCategoryMorphism( counit_images.(a) ) then
            counit_record_morphisms.(a) := counit_images.(a) * IdentityMorphism( B0.1 );
        fi;
    od;
    
    ## we know how to map the objects
    for a in vertices do
        counit_record_objects.(a) := B0.1;
    od;
   
    counit_functor := CapFunctor( B, counit_record_objects, counit_record_morphisms );
    
    if not IsIdenticalObj( B0, AsCapCategory( Range( counit_functor ) ) ) then
        Error( "counit_functor has a the wrong target category\n" );
    fi;
    
    SetCounit( B, counit_functor );
    
    B2 := B^2;
    
    # Construct the comultiplication as a CapFunctor from B to B^2
   
    comult_record_morphisms := ShallowCopy( comult_images );
    comult_record_objects := rec();
    
    ## we know how to map the objects
    for a in vertices do
        comult_record_objects.(a) := ElementaryTensor( B.(a), B.(a), B2 );
    od;
    
    comult_functor := CapFunctor( B, comult_record_objects, comult_record_morphisms );
    
    if not IsIdenticalObj( B2, AsCapCategory( Range( comult_functor ) ) ) then
        Error( "comult_functor has a the wrong target category\n" );
    fi;
    
    SetComultiplication( B, comult_functor );
    
    SetFilterObj( B, IsBialgebroid );
    
    return B;
    
end );

##
InstallMethod( Opposite,
        "for an algebroid",
        [ IsAlgebroid and HasUnderlyingQuiver ],
        
  function( A )
    local q_op, R, A_op;
    
    q_op := OppositeQuiver( UnderlyingQuiver( A ) );
    
    R := LeftActingDomain( UnderlyingQuiverAlgebra( A ) );
    
    A_op := Algebroid( R, q_op );
    
    SetOpposite( A_op, A );
    
    return A_op;
    
end );

##
InstallMethod( AddAntipode,
        "for a CAP category and a record",
        [ IsAlgebroid, IsRecord ],
        
  function( B, S_images_of_morphisms )
    local vertices, S_images_of_objects, S_functor, a;
    
    if IsAlgebraAsCategory(B) then
        B!.Name := Concatenation( "Hopf algebra", B!.Name{[ 10 .. Length( B!.Name ) ]} );
    else
        B!.Name := Concatenation( "Hopf algebroid", B!.Name{[ 10 .. Length( B!.Name ) ]} );
    fi;
    
    vertices := List( Vertices( UnderlyingQuiver( B ) ), String );
    
    S_images_of_objects := rec();
    for a in vertices do
        S_images_of_objects.(a) := B.(a);
    od;

    S_functor := CapFunctor( B, S_images_of_objects, S_images_of_morphisms );
    S_functor!.IsContravariant := true;
    
    S_functor!.Name := Concatenation( "Contravariant f", S_functor!.Name{[ 2 .. Length( S_functor!.Name ) ]} );
    
    if not IsIdenticalObj( B, AsCapCategory( Range( S_functor ) ) ) then
        Error( "S_functor has a the wrong target category\n" );
    fi;
    
    SetAntipode( B, S_functor );
    
end );

##
InstallMethod( IsCommutative,
        "for an Algebroid",
        [ IsAlgebroid ],
     
  function( A )
    local arrows, i, j;
    
    arrows := Arrows( UnderlyingQuiverAlgebra( A ) );
    
    for i in [ 1 .. Length(arrows) ] do
      for j in [ (i+1) .. Length(arrows) ] do
        if not arrows[i]*arrows[j] = arrows[j]*arrows[i] then
          return false;
        fi;
      od;
    od;
    
    return true;
end );

##
InstallMethod( IsCoassociative,
        "for an Algebroid",
        [ IsBialgebroid ],
    
  function( B )
    local vertices, comult, comult_times_id, id_times_comult, comult_times_id_after_comult, id_times_comult_after_comult, objects, morphisms, comult_times_id_after_comult_of_m, id_times_comult_after_comult_of_m, underlying_quiver_algebra_element_of_comult_times_id_after_comult_of_m, underlying_quiver_algebra_element_of_id_times_comult_after_comult_of_m, algebra_1, algebra_2, iso1, iso2, m;
    
    vertices := Vertices( UnderlyingQuiver( B ) );

    comult := Comultiplication(B);

    comult_times_id := TensorProductOnMorphisms( comult, IdentityFunctor(B) );
    id_times_comult := TensorProductOnMorphisms( IdentityFunctor(B), comult );
    
    comult_times_id_after_comult := PostCompose( comult_times_id, comult );
    id_times_comult_after_comult := PostCompose( id_times_comult, comult );

    objects := SetOfObjects(B);
    morphisms := SetOfGeneratingMorphisms(B);

    for m in morphisms do

        comult_times_id_after_comult_of_m := ApplyFunctor(comult_times_id_after_comult, m);
        id_times_comult_after_comult_of_m := ApplyFunctor(id_times_comult_after_comult, m);

        underlying_quiver_algebra_element_of_comult_times_id_after_comult_of_m := UnderlyingQuiverAlgebraElement(comult_times_id_after_comult_of_m);
        underlying_quiver_algebra_element_of_id_times_comult_after_comult_of_m := UnderlyingQuiverAlgebraElement(id_times_comult_after_comult_of_m);

        algebra_1 := AlgebraOfElement(underlying_quiver_algebra_element_of_comult_times_id_after_comult_of_m);
        algebra_2 := AlgebraOfElement(underlying_quiver_algebra_element_of_id_times_comult_after_comult_of_m);

        iso1 := IsomorphismToFlatTensorProduct(algebra_1);
        iso2 := IsomorphismToFlatTensorProduct(algebra_2);

        if not ImageElm( iso1, underlying_quiver_algebra_element_of_comult_times_id_after_comult_of_m) = ImageElm( iso2, underlying_quiver_algebra_element_of_id_times_comult_after_comult_of_m) then

            return false;

        fi;

    od;
    return true;
end );

##
InstallMethod( IsCounitary,
        "for a commutative bialgebra",
        [ IsAlgebraAsCategory and IsCommutative and IsBialgebroid ],
        
  function( B )
    local comult, counit, mult, unit, idB, comp1, comp2, objects, morphisms, o, m;

    comult := Comultiplication( B );
    counit := Counit( B );
    
    mult := Multiplication( B );
    unit := Unit( B );
    
    idB := IdentityFunctor( B );

    comp1 := PreCompose( [ comult, TensorProductOnMorphisms( counit, idB ), TensorProductOnMorphisms( unit, idB ), mult ] );
    comp2 := PreCompose( [ comult, TensorProductOnMorphisms( idB, counit ), TensorProductOnMorphisms( idB, unit ), mult ] );
    
    objects := SetOfObjects( B );
    morphisms := SetOfGeneratingMorphisms( B );
    
    for o in SetOfObjects( B ) do

        if not ( ( IsEqualForObjects( ApplyFunctor(comp1, o), o ) ) and ( IsEqualForObjects( ApplyFunctor(comp2, o), o ) ) ) then

            return false;

        fi;

    od;

    for m in SetOfGeneratingMorphisms( B ) do

        if not ( ( IsEqualForMorphisms( ApplyFunctor(comp1, m), m ) ) and ( IsEqualForMorphisms( ApplyFunctor(comp2, m), m ) ) ) then

            return false;

        fi;

    od;
    
    return true;
    
end );

##
InstallMethod( IsCocommutative,
         "for a bialgebra",
        [ IsBialgebroid ],
  function( B )
    local comult, twist_after_comult, objects, morphisms, o, m;
    
    comult := Comultiplication( B );
    
    twist_after_comult := PreCompose( [ comult, Twist( B, B ) ] );
    
    objects := SetOfObjects( B );
    morphisms := SetOfGeneratingMorphisms( B );
    
    for o in SetOfObjects( B ) do

        if not IsEqualForObjects( ApplyFunctor(twist_after_comult, o), ApplyFunctor(comult, o) ) then

            return false;

        fi;
 
    od;

    for m in SetOfGeneratingMorphisms( B ) do

        if not IsEqualForMorphisms( ApplyFunctor(twist_after_comult, m), ApplyFunctor(comult, m ) ) then

            return false;

        fi;

    od;
    
    return true;
end );

InstallMethod( IsHopfAlgebroid,
        "for commutative bialgebra",
        [ IsAlgebraAsCategory and IsCommutative and IsBialgebroid ],
  function( B )
    local comult, counit, antipode, mult, unit, idB, composition1, composition2, unit_after_counit, objects, morphisms, o, m;
    
    comult := Comultiplication( B );
    counit := Counit( B );
    
    mult := Multiplication( B );
    unit := Unit( B );

    antipode := Antipode( B );
    
    idB := IdentityFunctor( B );

    composition1 := PreCompose( [ comult, TensorProductOnMorphisms( antipode, idB ), mult ] );
    composition2 := PreCompose( [ comult, TensorProductOnMorphisms( idB, antipode ), mult ] );
    
    unit_after_counit := PreCompose( counit, unit);

    objects := SetOfObjects( B );
    morphisms := SetOfGeneratingMorphisms( B );
    
    for o in SetOfObjects( B ) do

        if not ( IsEqualForObjects( ApplyFunctor(composition1, o), ApplyFunctor(unit_after_counit, o) ) and IsEqualForObjects( ApplyFunctor(composition2, o), ApplyFunctor(unit_after_counit, o) ) ) then

            return false;

        fi;

    od;

    for m in SetOfGeneratingMorphisms( B ) do

        if not ( IsEqualForMorphisms( ApplyFunctor(composition1, m), ApplyFunctor(unit_after_counit, m ) ) and IsEqualForMorphisms( ApplyFunctor(composition2, m), ApplyFunctor(unit_after_counit, m) ) ) then

            return false;

        fi;

    od;
    
    return true;
end );

InstallMethod( Twist,
        "for two algebroids",
        [ IsAlgebroid, IsAlgebroid ],
  function( A, B )
    local A_underlying_quiver_algebra, B_underlying_quiver_algebra, AxB, BxA, A_objects, B_objects, A_morphisms, B_morphisms, a, b, record_with_images_of_objects, record_with_images_of_morphisms, axg, axg_string, fxb, fxb_string, g, f;
    
    A_underlying_quiver_algebra := UnderlyingQuiver( A );
    B_underlying_quiver_algebra := UnderlyingQuiver( B );
    
    AxB := A * B;
    BxA := B * A;
    
    A_objects := SetOfObjects( A );
    B_objects := SetOfObjects( B );
    
    A_morphisms := SetOfGeneratingMorphisms( A );
    B_morphisms := SetOfGeneratingMorphisms( B );
    
    record_with_images_of_objects := rec( );

    for a in A_objects do
      for b in B_objects do
        record_with_images_of_objects.( String( UnderlyingVertex( ElementaryTensor( a, b, AxB ) ) ) ) := ElementaryTensor( b, a, BxA );
      od;
    od;
    
    record_with_images_of_morphisms := rec( );
    
    for a in A_objects do
        for g in B_morphisms do
            axg := ElementaryTensor( a, g, AxB );
            axg_string := String( LeadingPath( Representative( UnderlyingQuiverAlgebraElement( axg ) ) ) );
            record_with_images_of_morphisms.(axg_string) := ElementaryTensor( g, a, BxA );
        od;
    od;
    
    for b in B_objects do
        for f in A_morphisms do
            fxb := ElementaryTensor( f, b, AxB );
            fxb_string := String( LeadingPath( Representative( UnderlyingQuiverAlgebraElement( fxb ) ) ) );
            record_with_images_of_morphisms.(fxb_string) := ElementaryTensor( b, f, BxA );
        od;
    od;
    
    return CapFunctor( AxB, record_with_images_of_objects, record_with_images_of_morphisms );

end);

##
InstallMethod( NaturalTransformation,
        "for a record and two CAP functors",
        [ IsRecord, IsCapFunctorRep, IsCapFunctorRep ],
        
  function( eta, F, G )
    local nat_tr;
    
    if NamesOfComponents( F ) = [ ] then
        Error( "the record of images is empty\n" );
    fi;
    
    if IsBound( eta.name ) then
        nat_tr := eta.name;
    else
        nat_tr := Concatenation( "Natural transformation from ", Name( F ), " -> ", Name( G ) );
    fi;
    
    nat_tr := NaturalTransformation( nat_tr, F, G );
    
    nat_tr!.defining_record := eta;
    
    AddNaturalTransformationFunction( nat_tr,
      function( source, obj, range )
        return eta.(String( UnderlyingVertex( obj ) ));
      end );
    
    return nat_tr;
    
end );

####################################
#
# View, Print, and Display methods:
#
####################################

##
InstallMethod( ViewObj,
        "for an object in an algebroid",
        [ IsCapCategoryObjectInAlgebroidRep ],

  function( o )
    
    ViewObj( UnderlyingVertex( o ) );
    
end );

##
InstallMethod( ViewObj,
        "for a morphism in an algebroid",
        [ IsCapCategoryMorphismInAlgebroidRep ],

  function( o )
    
    if IsRightQuiverAlgebra( UnderlyingQuiverAlgebra( CapCategory( o ) ) ) then
        ViewObj( Source( o ) );
        Print( "-[" );
        ViewObj( UnderlyingQuiverAlgebraElement( o ) );
        Print( "]->" );
        ViewObj( Range( o ) );
    else
        ViewObj( Range( o ) );
        Print( "<-[" );
        ViewObj( UnderlyingQuiverAlgebraElement( o ) );
        Print( "]-" );
        ViewObj( Source( o ) );
    fi;
    
end );
